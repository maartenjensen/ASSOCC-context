; Filename: context_state_sick_night.nls
; What we know from the context within these functions
; Location: Does not matter in implementation, as the agent just travels to the location where the agent can perform the action.
; Time: Night! Regardless of working day or non working day
; Age: All
; Sickness: The agent is sick
; Compliance is included

;Important needs (8 needs): 
;Sleep -> Rest at home
;Health -> Rest at home
;Risk avoidance -> Rest at home
;Belonging -> Rest at home (home, private leisure and public leisure give belonging however the agent should by default be at home when needing to sleep and when especially when sick)
;Autonomy -> Rest at home
;Compliance -> rest at home
;Conformity (conflict) -> Depends on the network
;Leisure (conflict) -> If critical, leisure at private leisure (since sick so doesn't want to go to public)

;Adjusted needs:
;None

;Code check:
; cssn: NOT csn, cso, csft, csso, cssft, csowh, cssowh

; QUESTION?? Should the agent always social distance when sick??

to-report cssn-function ; cssn
  ; The time is morning, afternoon or evening
  if ce-log-agent and who = ce-log-agent-id [ print "Sick night deliberation" ]

  ; -------------------------------------------
  ; Determine whether there is conflict
  ; -------------------------------------------
  if not cssn-is-need-salient-and-conflicting or ce-disable-conflict-checking
  [
    if ce-log-agent and who = ce-log-agent-id [ print "No conflict: performing default sick night" ]
    report cssn-default
  ]
  
  if ce-context-depth <= 1
  [ report cssn-after-minimal-context-f ]
  
  ; -------------------------------------------
  ; Determine whether there is a need salient
  ; -------------------------------------------
  let salient-needs-and-level cssn-salient-needs
  let salient-needs first salient-needs-and-level
  if ce-log-agent and who = ce-log-agent-id [ print (word "Salient needs:" salient-needs) ]
  
  if length salient-needs = 0
  [ error "in context-state-sick-night, salient-needs should be not 0" ]
  
  ; -------------------------------------------
  ; If salient needs is larger than 1, determine most salient
  ; -------------------------------------------
  let most-salient-need first salient-needs
  if length salient-needs > 1
  [
    if ce-context-depth < 2  [ report cssn-exit-before-multi-needs-f ] ; ===============  BREAKOUT: Strategic
    set most-salient-need (context-determine-most-salient-need salient-needs (last salient-needs-and-level))
  ]
  
  ; -------------------------------------------
  ; Deliberate on most salient need
  ; -------------------------------------------
  report cssn-salient-need-deliberation most-salient-need
end

to-report cssn-salient-need-deliberation [most-salient-need]
  if ce-log-agent and who = ce-log-agent-id [ print (word "Most salient need:" most-salient-need) ]
  
  ; Sleep is first checked since its most probable that this is the lowest need, then health since the agent is sick
  ; The following needs support the default action: resting at home
  if most-salient-need = need-sleep or most-salient-need = need-health or most-salient-need = need-belonging or most-salient-need = need-autonomy or most-salient-need = need-risk-avoidance or most-salient-need = need-compliance
  [ report cssn-sleep-health-belonging-autonomy-risk-avoidance-compliance ]
  
  ; The following needs support a non-default action and therefore the ce-need-critical-threshold needs to be checked. 
  ; Conformity is a little bit of an exception where if the network stays home the agent can just following conformity
  if most-salient-need = need-leisure [
    if ce-context-depth < 2  [ report cssn-exit-before-strategic-f ] ; ===============  BREAKOUT: Strategic
    report cssn-leisure
  ]
  
  if most-salient-need = need-conformity [
    if ce-context-depth < 4  [ report cssn-exit-before-conformity-f ] ; ===============  BREAKOUT: Conformity
    report cssn-conformity
  ]
  report cssn-error-f most-salient-need
end

;============== NEED FUNCTIONS =================
to-report cssn-default
  report context-determine-social-distancing (list my-home "rest")
end

to-report cssn-sleep-health-belonging-autonomy-risk-avoidance-compliance
  report context-determine-social-distancing (list my-home "rest")
end

to-report cssn-leisure
  ifelse leisure-satisfaction-level < ce-need-critical-threshold 
  [ report context-determine-social-distancing (activity-descriptor my-private-leisure "relaxing") ]
  [ report cssn-leisure-f ]
end

to-report cssn-conformity
  let network-action context-conformity-get-network-action
  ifelse empty? network-action
  [ if ce-log-agent and who = ce-log-agent-id [ print (word "Get network action: No action found") ] ]
  [
    let network-g-type (location-of network-action)
    let network-motive motive-of network-action
    let network-sd did-my-network-socially-distance? ; Since conformity is lower than risk-avoidance the agent will follow what the network does rather than social distancing based on the risk-avoidance value
    if ce-log-agent and who = ce-log-agent-id [ print (word "Get network action: " network-g-type ", " network-motive ", " network-sd) ]
    if network-g-type = home-gathering-type and network-motive = "rest"
    [ report (list my-home "rest" network-sd) ]
    
    if conformity-satisfaction-level < ce-need-critical-threshold ; Conformity is lower than compliance, so an agent can just do leisure
    [
      if network-g-type = public-leisure-gathering-type and network-motive = "relaxing"
      [ report lput network-sd (activity-descriptor my-public-leisure "relaxing") ]
      if network-g-type = private-leisure-gathering-type and network-motive = "relaxing"
      [ report lput network-sd (activity-descriptor my-private-leisure "relaxing") ]
    ]
    ; The action taking treatment is not affected by conformity, i.e. if the agent's network is all in treatment this should not make this agent go into treatment
    ; Working (at workplace or from home), school, university, or shopping are not possible actions during the night.
  ]
  report cssn-conformity-f
end

;============== FULL DELIBERATION FUNCTIONS ==================
to-report cssn-after-minimal-context-f
  report []
end

to-report cssn-exit-before-multi-needs-f
  report []
end

to-report cssn-exit-before-strategic-f
  report []
end

to-report cssn-exit-before-conformity-f
  report []
end

to-report cssn-leisure-f
  report []
end

to-report cssn-conformity-f
  report []
end

to-report cssn-error-f [most-salient-need]
  print (word "Error: sick night" most-salient-need)
  report []
end

;============== SALIENT NEEDS =================
to-report cssn-is-need-salient-and-conflicting
  if leisure-satisfaction-level < ce-need-salient-threshold or conformity-satisfaction-level < ce-need-salient-threshold
  [ report true ]
  report false
end

to-report cssn-salient-needs
  let salient-needs []
  let salient-needs-level []
  if sleep-satisfaction-level < ce-need-salient-threshold
  [ set salient-needs lput need-sleep salient-needs 
    set salient-needs-level lput sleep-satisfaction-level salient-needs-level ]
  if risk-avoidance-satisfaction-level < ce-need-salient-threshold
  [ set salient-needs lput need-risk-avoidance salient-needs 
    set salient-needs-level lput risk-avoidance-satisfaction-level salient-needs-level ]
  if belonging-satisfaction-level < ce-need-salient-threshold
  [ set salient-needs lput need-belonging salient-needs 
    set salient-needs-level lput belonging-satisfaction-level salient-needs-level ]
  if leisure-satisfaction-level < ce-need-salient-threshold
  [ set salient-needs lput need-leisure salient-needs 
    set salient-needs-level lput leisure-satisfaction-level salient-needs-level ]
  if autonomy-satisfaction-level < ce-need-salient-threshold
  [ set salient-needs lput need-autonomy salient-needs 
    set salient-needs-level lput autonomy-satisfaction-level salient-needs-level ]
  if conformity-satisfaction-level < ce-need-salient-threshold
  [ set salient-needs lput need-conformity salient-needs 
    set salient-needs-level lput conformity-satisfaction-level salient-needs-level ]
  if compliance-satisfaction-level < ce-need-salient-threshold
  [ set salient-needs lput need-compliance salient-needs 
    set salient-needs-level lput compliance-satisfaction-level salient-needs-level ]
  if health-satisfaction-level < ce-need-salient-threshold
  [ set salient-needs lput need-health salient-needs 
    set salient-needs-level lput health-satisfaction-level salient-needs-level ]
  report list salient-needs salient-needs-level
end