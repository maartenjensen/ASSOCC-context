globals [
  inspector-file
  inspected-youth
  inspected-student
  inspected-worker
  inspected-retiree
]

to setup-agent-inspector
  set inspector-file "inspector"
  if behaviorspace-experiment-name != "" [
    set inspector-file (word inspector-file "_" behaviorspace-experiment-name)
  ]
  set inspector-file (word inspector-file "_run" behaviorspace-run-number ".csv")
  if inspect-to-file? [
    set inspected-youth one-of youngs
    set inspected-student one-of students
    set inspected-worker one-of workers
    set inspected-retiree one-of retireds
    
    if file-exists? inspector-file [ file-delete inspector-file ]
    file-open inspector-file
    file-print "who, age, time, day, health, location, people-at-location, mode-of-transport, people-in-transport, queued"
    file-close
  ]
end

to inspect-agents
  if inspect-to-file? [
    if inspected-youth = nobody and any? youngs [ set inspected-youth one-of youngs ]
    if inspected-student = nobody and any? students [ set inspected-student one-of students ]
    if inspected-worker = nobody and any? workers [ set inspected-worker one-of workers ]
    if inspected-retiree = nobody and any? retireds [ set inspected-retiree one-of retireds ]
    file-open inspector-file
    if inspected-youth != nobody [
      ask inspected-youth   [ inspect-agent ]
    ]
    if inspected-student != nobody [
      ask inspected-student [ inspect-agent ]
    ]
    if inspected-worker != nobody [
      ask inspected-worker  [ inspect-agent ]
    ]
    if inspected-retiree != nobody [
      ask inspected-retiree [ inspect-agent ]
    ]
    file-close
  ]
end

to inspect-agent
  file-print (word
    who ","
    age ","
    slice-of-the-day ","
    day-of-the-week ","
    infection-status ","
    [gathering-type] of current-activity ","
    ([count current-gatherers] of current-activity) ","
    my-means-of-transportation-I-currently-use ","
    #people-in-my-transport ","
    stayed-out-queuing-for-bus?)
end